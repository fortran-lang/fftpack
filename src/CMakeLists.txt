#### Pre-process: .fpp -> .f90 via Fypp

# Create a list of the files to be preprocessed
# set(fppFiles
#     stdlib_version.fypp
# )

# Preprocessed files to contain preprocessor directives -> .F90
# set(cppFiles
#     stdlib_linalg_constants.fypp
# )

# fypp_f90("${fyppFlags}" "${fppFiles}" outFiles)
# fypp_f90pp("${fyppFlags}" "${cppFiles}" outPreprocFiles)

set(SRC
    fftpack_kinds.f90
    fftpack_legacy_drivers_pass.f90
    fftpack_legacy_drivers_rad.f90
    cfftb1.f90
    cfftf1.f90
    cffti1.f90
    cosqb1.f90
    cosqf1.f90
    dcosqb.f90
    dcosqf.f90
    dcosqi.f90
    dcost.f90
    dcosti.f90
    dfftb.f90
    dfftf.f90
    dffti.f90
    dsinqb.f90
    dsinqf.f90
    dsinqi.f90
    dsint.f90
    dsinti.f90
    dzfftb.f90
    dzfftf.f90
    dzffti.f90
    ezfft1.f90
    fftpack_dct.f90
    fftpack.f90
    fftpack_fft.f90
    fftpack_fftshift.f90
    fftpack_ifft.f90
    fftpack_ifftshift.f90
    fftpack_irfft.f90
    fftpack_rfft.f90
    fftpack_utils.f90
    rfftb1.f90
    rfftf1.f90
    rffti1.f90
    sint1.f90
    zfftb.f90
    zfftf.f90
    zffti.f90
    ${outFiles}
    ${outPreprocFiles}
)

add_library(${PROJECT_NAME} ${SRC})

set_target_properties(
  ${PROJECT_NAME}
  PROPERTIES
  POSITION_INDEPENDENT_CODE ON
  WINDOWS_EXPORT_ALL_SYMBOLS ON
)

if(CMAKE_Fortran_COMPILER_ID STREQUAL GNU AND CMAKE_Fortran_COMPILER_VERSION VERSION_LESS 10.0)
  target_compile_options(
     ${PROJECT_NAME}
     PRIVATE
     $<$<COMPILE_LANGUAGE:Fortran>:-fno-range-check>
   )
endif()

set(LIB_MOD_DIR ${CMAKE_CURRENT_BINARY_DIR}/mod_files/)
# We need the module directory before we finish the configure stage since the
# build interface might resolve before the module directory is generated by CMake
if(NOT EXISTS "${LIB_MOD_DIR}")
  file(MAKE_DIRECTORY "${LIB_MOD_DIR}")
endif()

set_target_properties(${PROJECT_NAME} PROPERTIES
    Fortran_MODULE_DIRECTORY ${LIB_MOD_DIR})
target_include_directories(${PROJECT_NAME} PUBLIC
    $<BUILD_INTERFACE:${LIB_MOD_DIR}>
    $<INSTALL_INTERFACE:${CMAKE_INSTALL_MODULEDIR}>
)

install(TARGETS ${PROJECT_NAME}
        EXPORT ${PROJECT_NAME}-targets
        RUNTIME DESTINATION "${CMAKE_INSTALL_BINDIR}"
        ARCHIVE DESTINATION "${CMAKE_INSTALL_LIBDIR}"
        LIBRARY DESTINATION "${CMAKE_INSTALL_LIBDIR}"
)
install(DIRECTORY ${LIB_MOD_DIR} DESTINATION "${CMAKE_INSTALL_MODULEDIR}")
